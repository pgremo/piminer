# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: monson <holymonson@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: David Manouchehri <david@davidmanouchehri.com>

pkgname=bfgminer
pkgver=3.10.0
pkgrel=1
pkgdesc="Bitcoin miner featuring overclocking, monitoring, fan speed control and remote management. For FPGA/GPU/CPU Bitcoin mining."
arch=('i686' 'x86_64' 'armv6h')
depends=('curl' 'jansson' 'libevent' 'libmicrohttpd' 'libusbx' 'lm_sensors')
makedepends=('uthash' 'yasm')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA')
url='https://bitcointalk.org/index.php?topic=168174.0'
license=('GPL3')
source=("http://luke.dashjr.org/programs/bitcoin/files/$pkgname/$pkgver/$pkgname-$pkgver.tbz2"
  "remove-dangerous-rpath.patch"
        "$pkgname.conf.d"
        "$pkgname.service")
backup=("etc/conf.d/$pkgname" "etc/$pkgname.conf")

[ "$CARCH" == "x86_64" ] && makedepends+=('yasm')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -p0 < "${srcdir}"/remove-dangerous-rpath.patch
  NOSUBMODULES=1 ./autogen.sh
}


build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr \
    --enable-cpumining \
    --enable-bfsb \
    --enable-metabank \
    --enable-scrypt \
    --enable-opencl \
    --with-udevrulesdir=/usr/lib/udev/rules.d
  # --enable-knc (FS#33640)
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -Dm644 "$srcdir"/$pkgname.conf.d "$pkgdir"/etc/conf.d/$pkgname
  sed 's#/usr/local/bin#/usr/bin#g' example.conf > $pkgname.conf
  install -Dm644 $pkgname.conf "$pkgdir"/etc/$pkgname.conf
}
sha512sums=('92b69de429522b82b24eef1290cb587705cac2a1122aa80a1239fd45f4a68c2d2f154655db3a291d9b3eda9e63f6608ad3a120b5ec9250f569d95f9ae4f7aff3'
            '4feb442588a33033422279b22daa8b55eab20e68d13cd209c33533cd611b27dac3ab4870c88e6b9802cc3bc74269d7e1e7531a70c9105ca791dc47fca2699dae'
            'df4b9be1221f51e48b9d673b376810cc87cae5bf92016050e316aaadfbd39bbe9ab3e57b822abd5e87bb2e0f03ddd9990e8bb250ac6c3667a0f33ec43c8d9d98'
            '90796d9601f1e8a2422a99c75e0147b77fa9fba4af3ad2b1224c2ceec7064a908ac8d220448309e94dce5a664944bfe3fd46ec6b11eb9d6f296bac5b3ed5008d')
